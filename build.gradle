plugins {
    // Unlike most projects, we choose to pin the specific version of Loom.
    // This prevents a lot of issues where the build script can fail randomly because the Fabric Maven server
    // is not reachable for some reason, and it makes builds much more reproducible. Observation also shows that it
    // really helps to improve startup times on slow connections.
    id 'fabric-loom' version '1.5.7'
}

apply plugin: "fabric-loom"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = createVersionString()
group = project.maven_group

loom {
    mixin {
        defaultRefmapName = "mixins.sodium.refmap.json"
    }

    accessWidenerPath = file("src/main/resources/sodium.accesswidener")
}

configurations {
    modIncludeImplementation

    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

sourceSets {
    api {
        java {
            compileClasspath += main.compileClasspath
        }
    }

    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }

    // this source set is should be compiled with Java 8
    desktop {
        java {
            srcDir "src/desktop/java"
        }
    }
}

dependencies {
    // Sourced from the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modIncludeImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-block-view-api-v2", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-rendering-data-attachment-v1", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))
}

jar {
    // License files
    from "${rootProject.projectDir}/COPYING"
    from "${rootProject.projectDir}/COPYING.LESSER"

    // API files
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir

    // Desktop application files
    from sourceSets.desktop.output.classesDirs
    from sourceSets.desktop.output.resourcesDir

    manifest.attributes["Main-Class"] = "${project.main_class}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.named('compileDesktopJava') {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

def createVersionString() {
    var builder = new StringBuilder()

    boolean release = project.hasProperty("build.release")

    String build_id = System.getenv("GITHUB_RUN_NUMBER")
    String mod_version = project.mod_version as String

    if (release) {
        builder.append(mod_version)
    } else {
        // Strip the existing pre-release version
        if (mod_version.contains('-')) {
            builder.append(mod_version.substring(0, mod_version.indexOf('-')))
        } else {
            builder.append(mod_version)
        }

        builder.append("-snapshot")
    }

    var minecraft_version = (project.minecraft_version as String)
    builder.append("+mc").append(minecraft_version)

    if (!release) {
        if (build_id != null) {
            builder.append("-build.${build_id}")
        } else {
            builder.append("-local")
        }
    }

    return builder.toString()
}